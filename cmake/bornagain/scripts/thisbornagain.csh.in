# thisbornagain.csh is generated by cmake from thisbornagain.csh.in

# Purpose:
#    set environment variables like LD_LIBRARY_PATH and PYTHONPATH
#    to include the directory the current BornAgain is installed in

# Usage:
#    source <path>/thisbornagain.sh  # under sh, bash, ...
#    source <path>/thisbornagain.csh # under csh

# History:
#    adapted from a CERN Root script by Fons Rademakers, 18/8/2006
#    by the BornAgain team at JCNS/MLZ 2013-

if ($?BORNAGAINSYS) then
   set old_bornagain="$BORNAGAINSYS"
endif

# $_ should be source .../thisbornagain.csh
set ARGS=($_)
if ("$ARGS" != "") then
   set thisbornagain="`dirname ${ARGS[2]}`"
else
   # But $_ might not be set if the script is source non-interactively.
   # In [t]csh the sourced file is inserted 'in place' inside the
   # outer script, so we need an external source of information
   # either via the current directory or an extra parameter.
   if ( -e thisbornagain.csh ) then
      set thisbornagain=${PWD}
   else if ( -e bin/thisbornagain.csh ) then
      set thisbornagain=${PWD}/bin
   else if ( "$1" != "" ) then
      if ( -e ${1}/bin/thisbornagain.csh ) then
         set thisbornagain=${1}/bin
      else if ( -e ${1}/thisbornagain.csh ) then
         set thisbornagain=${1}
      else
         echo "thisbornagain.csh: ${1} does not contain a BornAgain installation"
      endif
   else
      echo 'Error: The call to "source where_bornagain_is/bin/thisbornagain.csh" can not determine the location of the BornAgain installation'
      echo "because it was embedded another script (this is an issue specific to csh)."
      echo "Use either:"
      echo "   cd where_bornagain_is; source bin/thisbornagain.csh"
      echo "or"
      echo "   source where_bornagain_is/bin/thisbornagain.csh where_bornagain_is"
   endif
endif

if ($?thisbornagain) then

setenv BORNAGAINSYS "`(cd ${thisbornagain}/..;pwd)`"

if ($?old_bornagain) then
   setenv PATH `echo $PATH | sed -e "s;:$old_bornagain/bin:;:;g" \
                                 -e "s;:$old_bornagain/bin;;g"   \
                                 -e "s;$old_bornagain/bin:;;g"   \
                                 -e "s;$old_bornagain/bin;;g"`
   if ($?LD_LIBRARY_PATH) then
      setenv LD_LIBRARY_PATH `echo $LD_LIBRARY_PATH | \
                             sed -e "s;:$old_bornagain/lib:;:;g" \
                                 -e "s;:$old_bornagain/lib;;g"   \
                                 -e "s;$old_bornagain/lib:;;g"   \
                                 -e "s;$old_bornagain/lib;;g"`
   endif
   if ($?DYLD_LIBRARY_PATH) then
      setenv DYLD_LIBRARY_PATH `echo $DYLD_LIBRARY_PATH | \
                             sed -e "s;:$old_bornagain/lib:;:;g" \
                                 -e "s;:$old_bornagain/lib;;g"   \
                                 -e "s;$old_bornagain/lib:;;g"   \
                                 -e "s;$old_bornagain/lib;;g"`
   endif
   if ($?SHLIB_PATH) then
      setenv SHLIB_PATH `echo $SHLIB_PATH | \
                             sed -e "s;:$old_bornagain/lib:;:;g" \
                                 -e "s;:$old_bornagain/lib;;g"   \
                                 -e "s;$old_bornagain/lib:;;g"   \
                                 -e "s;$old_bornagain/lib;;g"`
   endif
   if ($?LIBPATH) then
      setenv LIBPATH `echo $LIBPATH | \
                             sed -e "s;:$old_bornagain/lib:;:;g" \
                                 -e "s;:$old_bornagain/lib;;g"   \
                                 -e "s;$old_bornagain/lib:;;g"   \
                                 -e "s;$old_bornagain/lib;;g"`
   endif
   if ($?PYTHONPATH) then
      setenv PYTHONPATH `echo $PYTHONPATH | \
                             sed -e "s;:$old_bornagain/lib:;:;g" \
                                 -e "s;:$old_bornagain/lib;;g"   \
                                 -e "s;$old_bornagain/lib:;;g"   \
                                 -e "s;$old_bornagain/lib;;g"`
   endif
#   if ($?MANPATH) then
#      setenv MANPATH `echo $MANPATH | \
#                             sed -e "s;:$old_bornagain/man:;:;g" \
#                                 -e "s;:$old_bornagain/man;;g"   \
#                                 -e "s;$old_bornagain/man:;;g"   \
#                                 -e "s;$old_bornagain/man;;g"`
#   endif
endif



set path = (@this_bindir@ $path)

if ($?LD_LIBRARY_PATH) then
   setenv LD_LIBRARY_PATH @this_libdir@:$LD_LIBRARY_PATH      # Linux, ELF HP-UX
else
   setenv LD_LIBRARY_PATH @this_libdir@
endif

if ($?DYLD_LIBRARY_PATH) then
   setenv DYLD_LIBRARY_PATH @this_libdir@:$DYLD_LIBRARY_PATH  # Mac OS X
else
   setenv DYLD_LIBRARY_PATH @this_libdir@
endif

if ($?SHLIB_PATH) then
   setenv SHLIB_PATH @this_libdir@:$SHLIB_PATH                # legacy HP-UX
else
   setenv SHLIB_PATH @this_libdir@
endif

if ($?LIBPATH) then
   setenv LIBPATH @this_libdir@:$LIBPATH                      # AIX
else
   setenv LIBPATH @this_libdir@
endif

if ($?PYTHONPATH) then
   setenv PYTHONPATH @this_libdir@:$PYTHONPATH
else
   setenv PYTHONPATH @this_libdir@
endif


endif # if ("$thisbornagain" != "")

set thisbornagain=
set old_bornagain=
